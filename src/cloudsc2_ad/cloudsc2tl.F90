! Copyright (C) 2003- ECMWF
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!
SUBROUTINE CLOUDSC2TL ( &
 & KIDIA , KFDIA , KLON , KTDIA , KLEV, LDRAIN1D,&
 & PTSPHY,&
 & PAPHP15 , PAPP15  , PQM15   , PQS5   , PTM15,  PL5 ,  PI5,&
 & PLUDE5  , PLU5    , PMFU5   , PMFD5,&
 & PTENT5  , PGTENT5 , PTENQ5  , PGTENQ5 ,&
 & PTENL5  , PGTENL5 , PTENI5  , PGTENI5 , PSUPSAT5, &
 & PCLC5   , PFPLSL5 , PFPLSN5 ,&
 & PFHPSL5 , PFHPSN5 , PCOVPTOT5,&
 & PAPHP1 , PAPP1    , PQM1   , PQS   , PTM1,  PL ,  PI,&
 & PLUDE  , PLU      , PMFU   , PMFD,&
 & PTENT  , PGTENT   , PTENQ  , PGTENQ ,&
 & PTENL  , PGTENL   , PTENI  , PGTENI , PSUPSAT, &
 & PCLC   , PFPLSL   , PFPLSN ,&
 & PFHPSL , PFHPSN   , PCOVPTOT )  

!**** *CLOUDSC2TL*  - COMPUTES CLOUD COVER, CLOUD LIQUID WATER/ICE
!                     AND LARGE-SCALE CONDENSATION.
!                     PROGNOSTIC CLOUD LIQUID WATER/ICE VARIABLES.
!                     (tangent linear)

!     PURPOSE.
!     --------
!         THIS ROUTINE COMPUTES CLOUD AMOUNTS WHICH ARE REQUIRED BY THE
!     RADIATION SCHEME FOR COMPUTATION OF THE FLUXES AND THE PHYSICAL
!     TENDENCIES OF THE PROGNOSTIC VARIABLES T, Q, QL AND QI DUE TO
!     THE WATER PHASE CHANGES

!**   INTERFACE.
!     ----------
!              *CLOUDSC2TL* IS CALLED FROM *CALLPARTL*

!     PARAMETER     DESCRIPTION                                   UNITS
!     ---------     -----------                                   -----

!     INPUT PARAMETERS (INTEGER):

!    *KIDIA*        START POINT
!    *KFDIA*        END POINT
!    *KLON*         NUMBER OF GRID POINTS PER PACKET
!    *KTDIA*        START OF THE VERTICAL LOOP
!    *KLEV*         NUMBER OF LEVELS

!    INPUT PARAMETERS (REAL):

!    *PTSPHY*       TIME STEP FOR THE PHYSICS                     S
!    *PQM15*        SPECIFIC HUMIDITY AT T-1                  (Trajectory)
!    *PQS5*         SATURATION SPECIFIC HUMIDITY              (Trajectory)
!    *PTM15*        TEMPERATURE AT T-1                        (Trajectory)
!    *PL5*          CLOUD LIQUID WATER                        (Trajectory) 
!    *PI5*          CLOUD ICE                                 (Trajectory)  
!    *PAPP15*       PROVISIONAL PRESSURE ON FULL LEVELS       (Trajectory)
!    *PAPHP15*      PRESSURE AT HALF LEVELS AT T-1            (Trajectory)
!    *PLUDE5*       DETRAINED LIQUID WATER                    (Trajectory)
!    *PLU5*         LIQUID WATER CONTENT IN UPDRAFTS          (Trajectory)
!    *PMFU5*        MASS FLUX IN CONVECTIVE UPDRAFTS          (Trajectory)
!    *PMFD5*        MASS FLUX IN CONVECTIVE DOWNDRAFTS         (Trajectory)

!    *PQM1*         SPECIFIC HUMIDITY AT T-1                     KG/KG
!    *PQS*          SATURATION SPECIFIC HUMIDITY                 KG/KG
!    *PTM1*         TEMPERATURE AT T-1                             K
!    *PL*           CLOUD LIQUID WATER                           KG/KG       
!    *PI*           CLOUD ICE                                    KG/KG       
!    *PAPHP1*       PRESSURE AT HALF LEVELS AT T-1                PA   
!    *PAPP1*        PROVISIONAL PRESSURE ON FULL LEVELS           PA
!    *PLUDE*        DETRAINED LIQUID WATER                       KG/(M3*S)
!    *PLU*          LIQUID WATER CONTENT IN UPDRAFTS             KG/KG
!    *PMFU*         MASS FLUX IN CONVECTIVE UPDRAFTS             KG/(M2*S)
!    *PMFD*         MASS FLUX IN CONVECTIVE DOWNDRAFTS           KG/(M2*S)

!    UPDATED PARAMETERS (REAL):

!    *PTENT5*       TEMPERATURE TENDENCY                      (Trajectory)
!    *PTENQ5*       MOISTURE TENDENCY                         (Trajectory)
!    *PTENL5*       CLOUD LIQUID WATER TENDENCY               (Trajectory)
!    *PTENI5*       CLOUD ICE TENDENCY                        (Trajectory)
   
!    *PTENT*        TEMPERATURE TENDENCY                          K/S
!    *PTENQ*        MOISTURE TENDENCY                            KG/(KG S)
!    *PTENL*        CLOUD LIQUID WATER TENDENCY                  KG/(KG S)      
!    *PTENI*        CLOUD ICE  TENDENCY                          KG/(KG S)      

!    OUTPUT PARAMETERS (REAL):

!    *PCLC5*        CLOUD COVER OF INDIVIDUAL LAYER
!    *PFHPSL5*      ENTHALPY FLUX DUE TO LARGE SCALE RAIN     (Trajectory) 
!    *PFHPSN5*      ENTHALPY FLUX DUE TO LARGE SCALE SNOW     (Trajectory) 
!    *PFPLSL5*      LARGE SCALE RAIN FLUX                     (Trajectory)
!    *PFPLSN5*      LARGE SCALE SNOW FLUX                     (Trajectory) 
!    *PCOVPTOT5*    PRECIPITATION FRACTION IN EACH LAYER      (Trajectory) 
 
!    *PCLC*         CLOUD COVER OF INDIVIDUAL LAYER
!    *PFHPSL*       ENTHALPY FLUX DUE TO LARGE SCALE RAIN         J/(M2*S)
!    *PFHPSN*       ENTHALPY FLUX DUE TO LARGE SCALE SNOW         J/(M2*S)
!    *PFPLSL*       LARGE SCALE RAIN FLUX                        KG/(M2*S)
!    *PFPLSN*       LARGE SCALE SNOW FLUX                        KG/(M2*S)
!    *PCOVPTOT*     PRECIPITATION FRACTION IN EACH LAYER

!     METHOD.
!     -------

!     EXTERNALS.
!     ----------

!     REFERENCE.
!     ----------

!     AUTHOR.
!     -------
!     2009-07-23  P. Lopez

!     MODIFICATIONS.
!     --------------
!     F. Vana  01-Sep-2020: Introduced to CY46R1

!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
!USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RETV, RG  ,RCPD     ,&
 & RLVTT    ,RLSTT    ,RLMLT    ,RTT     ,RD  
USE YOETHF   , ONLY : R2ES     ,R3LES    ,R3IES    ,R4LES    ,&
 & R4IES    ,R5LES    ,R5IES    ,R5ALVCP  ,R5ALSCP ,&
 & RALVDCP  ,RALSDCP  ,RTWAT    ,RTICE    ,RTICECU ,&
 & RTWAT_RTICE_R      ,RTWAT_RTICECU_R    ,RVTMP2 
USE YOECLD   , ONLY : YRECLD
USE YOECLDP  , ONLY : YRECLDP
USE YOEPHLI  , ONLY : YREPHLI
USE YOPHNC   , ONLY : YRPHNC
USE YOMNCL   , ONLY : YRNCL

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
LOGICAL           ,INTENT(IN)    :: LDRAIN1D
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTSPHY 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHP15(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPP15(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQM15(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTM15(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PL5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PI5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLUDE5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLU5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMFU5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMFD5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENT5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENQ5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENL5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENI5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENT5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENQ5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENL5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENI5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSUPSAT5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCLC5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL5(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN5(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPSL5(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPSN5(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOVPTOT5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHP1(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPP1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLUDE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMFU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMFD(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSUPSAT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCLC(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPSL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPSN(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOVPTOT(KLON,KLEV)

!     -----------------------------------------------------------------

!*       0.1   ARGUMENTS.
!              ----------

!     -----------------------------------------------------------------

!*       0.2   LOCAL ARRAYS.
!              -------------

INTEGER(KIND=JPIM) :: JK, JL

!=======================================================================
!     TUNABLE CONSTANTS (to be moved to include files later)
!=======================================================================

! zscal is a scale factor that linearly reduces the variance between 
! qv=qv-crit and qv-qsat
! 0 = No scaling
! 1 = full scaling (i.e. variance=0 when qv=qsat)

REAL(KIND=JPRB) :: ZSCAL=0.9_JPRB

!=======================================================================

REAL(KIND=JPRB) :: ZTP15(KLON,KLEV), ZQP15(KLON,KLEV)
REAL(KIND=JPRB) :: ZL5(KLON,KLEV), ZI5(KLON,KLEV)
REAL(KIND=JPRB) :: ZLUDE5(KLON,KLEV)
REAL(KIND=JPRB) :: ZQC5(KLON,KLEV), ZQLWC5(KLON,KLEV), ZQIWC5(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFL5(KLON), ZSFL5(KLON), ZDP5(KLON,KLEV)
REAL(KIND=JPRB) :: ZLSDCP5(KLON,KLEV), ZLFDCP5(KLON,KLEV), ZLVDCP5(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFLN5(KLON), ZSFLN5(KLON), ZGDP5(KLON)
REAL(KIND=JPRB) :: ZDQDT5(KLON), ZDTDT5(KLON), ZDLDT5(KLON), ZDIDT5(KLON)
REAL(KIND=JPRB) :: ZQCRIT5(KLON), ZCOVPTOT5(KLON), ZCOVPCLR5(KLON)
REAL(KIND=JPRB) :: ZDQSDTEMP5(KLON), ZCORQS5(KLON), ZDTGDP5(KLON)
REAL(KIND=JPRB) :: ZQOLD5(KLON),ZPP5(KLON),ZDQ5(KLON), ZQLIM5(KLON)
REAL(KIND=JPRB) :: ZRFREEZE5(KLON,KLEV)
REAL(KIND=JPRB) :: ZCONDL5(KLON,KLEV), ZCONDI5(KLON,KLEV)
REAL(KIND=JPRB) :: ZEVAPR5(KLON,KLEV), ZEVAPS5(KLON,KLEV)
REAL(KIND=JPRB) :: ZQSAT5(KLON), ZFWAT5(KLON), ZFOEEW5(KLON)

REAL(KIND=JPRB) :: ZTP1(KLON,KLEV), ZQP1(KLON,KLEV)
REAL(KIND=JPRB) :: ZL(KLON,KLEV), ZI(KLON,KLEV)
REAL(KIND=JPRB) :: ZLUDE(KLON,KLEV)
REAL(KIND=JPRB) :: ZQC(KLON,KLEV), ZQLWC(KLON,KLEV), ZQIWC(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFL(KLON), ZSFL(KLON), ZDP(KLON,KLEV)
REAL(KIND=JPRB) :: ZLSDCP(KLON,KLEV), ZLFDCP(KLON,KLEV), ZLVDCP(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFLN(KLON), ZSFLN(KLON), ZGDP(KLON)
REAL(KIND=JPRB) :: ZDQDT(KLON), ZDTDT(KLON), ZDLDT(KLON), ZDIDT(KLON)
REAL(KIND=JPRB) :: ZQCRIT(KLON), ZCOVPTOT(KLON), ZCOVPCLR(KLON)
REAL(KIND=JPRB) :: ZDQSDTEMP(KLON), ZCORQS(KLON), ZDTGDP(KLON)
REAL(KIND=JPRB) :: ZQOLD(KLON),ZPP(KLON),ZDQ(KLON), ZQLIM(KLON)
REAL(KIND=JPRB) :: ZRFREEZE(KLON,KLEV)
REAL(KIND=JPRB) :: ZCONDL(KLON,KLEV), ZCONDI(KLON,KLEV)
REAL(KIND=JPRB) :: ZEVAPR(KLON,KLEV), ZEVAPS(KLON,KLEV)
REAL(KIND=JPRB) :: ZQSAT(KLON), ZFWAT(KLON), ZFOEEW(KLON)

REAL(KIND=JPRB) :: ZCRH(KLEV),ZSCALM(KLEV)

REAL(KIND=JPRB) :: ZOEALFA5, ZCLDL5, ZCLDI5, ZLNEW5, ZINEW5, ZPRR5, ZPRS5, ZD5, ZZ2S5
REAL(KIND=JPRB) :: ZCONS5, ZSNMLT5, ZZZ5, ZRN5, ZSN5, ZDR5, ZDR25
REAL(KIND=JPRB) :: ZOEALFAW5, ZFAC5, ZFACW5, ZFACI5, ZESDP5, ZCOR5
REAL(KIND=JPRB) :: ZPRTOT5, ZDPR5, ZDPRSFL5, ZDPSSFL5, ZPRECLR5
REAL(KIND=JPRB) :: ZQE5, ZBETA5, ZB5, ZQPD5, ZQCD5, ZSQRT5, ZFWATR5
REAL(KIND=JPRB) :: ZEXPDL5, ZEXPDI5, ZEXP15, ZEXP25, ZEXP35
REAL(KIND=JPRB) :: ZRFREEZE25, ZQT5
REAL(KIND=JPRB) :: ZRHO5,ZRODQSDP5,ZLDCP5,DTDZMO5,ZDQSDZ5,ZDQC5 
REAL(KIND=JPRB) :: ZSUPSAT5

REAL(KIND=JPRB) :: ZOEALFA, ZCLDL, ZCLDI, ZLNEW, ZINEW, ZPRR, ZPRS, ZLCRIT, ZD, ZZ2S
REAL(KIND=JPRB) :: ZCONS, ZSNMLT, ZZZ, ZRN, ZSN, ZDR, ZDR2
REAL(KIND=JPRB) :: ZOEALFAW, ZFAC, ZFACW, ZFACI, ZESDP, ZCOR
REAL(KIND=JPRB) :: ZPRTOT, ZDPR, ZDPRSFL, ZDPSSFL, ZPRECLR
REAL(KIND=JPRB) :: ZQE, ZBETA, ZB, ZQPD, ZQCD, ZFWATR
REAL(KIND=JPRB) :: ZRFREEZE2, ZQT
REAL(KIND=JPRB) :: ZRHO,ZRODQSDP,ZLDCP,DTDZMO,ZDQSDZ,ZDQC 
REAL(KIND=JPRB) :: ZFAC1, ZFAC2, ZFAC3, ZFAC4
REAL(KIND=JPRB) :: ZSUPSAT 

REAL(KIND=JPRB) :: ZCKCODTL, ZCKCODTI, ZCONS2, ZCONS3, ZMELTP2
REAL(KIND=JPRB) :: Z3ES, Z4ES, ZQMAX, ZQTMST

REAL(KIND=JPRB) :: ZRH1,ZRH2,ZRH3,ZETA2,ZDETA1,ZDETA2 
REAL(KIND=JPRB) :: ZTRPAUS(KLON)
REAL(KIND=JPRB) :: ZCRH2,ZETA3

REAL(KIND=JPRB) :: ZEPS1,ZEPS2,ZCKCODTLA,ZCKCODTIA

REAL(KIND=JPRB) :: ZYYY, ZRAT

INTEGER(KIND=JPIM) :: IK,ICALL

LOGICAL :: LLO1, LLO2, LLO3, LLFLAG(KLON)
!REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "cuadjtqstl.intfb.h"

!     ------------------------------------------------------------------
#include "fcttre.func.h"
#include "fcttretl.func.h"
!     ------------------------------------------------------------------

!IF (LHOOK) CALL DR_HOOK('CLOUDSC2TL',0,ZHOOK_HANDLE)
ASSOCIATE(CETA=>YRECLD%CETA, &
 & RCLCRIT=>YRECLDP%RCLCRIT, RKCONV=>YRECLDP%RKCONV, RLMIN=>YRECLDP%RLMIN, &
 & RPECONS=>YRECLDP%RPECONS, &
 & RLPTRC=>YREPHLI%RLPTRC, &
 & LREGCL=>YRNCL%LREGCL, &
 & LEVAPLS2=>YRPHNC%LEVAPLS2)

!*         1.     SET-UP INPUT QUANTITIES
!                 -----------------------

!*         1.1    Set-up tunning parameters

! set up constants required

ZCKCODTL=2.0_JPRB*RKCONV*PTSPHY
ZCKCODTI=5.0_JPRB*RKCONV*PTSPHY
ZCKCODTLA=ZCKCODTL/100._JPRB
ZCKCODTIA=ZCKCODTI/100._JPRB
ZCONS2 =1.0_JPRB/(PTSPHY*RG)
ZCONS3 =RLVTT/RCPD
ZMELTP2=RTT+2.0_JPRB
ZQTMST=1.0_JPRB/PTSPHY

ZQMAX=0.5_JPRB
ZEPS1=1.E-12_JPRB
ZEPS2=1.E-10_JPRB

!     --------------------------------------------------------------------

!*         2.1    COMPUTE CRITICAL RELATIVE HUMIDITY AND RELATIVE HUMIDITY
!                 --------------------------------------------------------

! first guess values for T, q, ql and qi

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    ZTP1 (JL,JK) = PTM1 (JL,JK) + PTSPHY*PGTENT (JL,JK)
    ZTP15(JL,JK) = PTM15(JL,JK) + PTSPHY*PGTENT5(JL,JK)
    ZQP1 (JL,JK) = PQM1 (JL,JK) + PTSPHY*PGTENQ (JL,JK) + PSUPSAT (JL,JK)
    ZQP15(JL,JK) = PQM15(JL,JK) + PTSPHY*PGTENQ5(JL,JK) + PSUPSAT5(JL,JK)
    ZL   (JL,JK) = PL   (JL,JK) + PTSPHY*PGTENL (JL,JK)
    ZL5  (JL,JK) = PL5  (JL,JK) + PTSPHY*PGTENL5(JL,JK)
    ZI   (JL,JK) = PI   (JL,JK) + PTSPHY*PGTENI (JL,JK)
    ZI5  (JL,JK) = PI5  (JL,JK) + PTSPHY*PGTENI5(JL,JK)
  ENDDO
ENDDO

DO JK=1,KLEV

! Parameter for cloud formation

  ZSCALM(JK)=ZSCAL*MAX((CETA(JK)-0.2_JPRB),ZEPS1)**(0.2_JPRB)

  DO JL=KIDIA,KFDIA

! thermodynamic constants

    ZDP (JL,JK) = PAPHP1(JL,JK+1)-PAPHP1(JL,JK)
    ZDP5(JL,JK) = PAPHP15(JL,JK+1)-PAPHP15(JL,JK)
    ZZZ  = -RCPD*RVTMP2*ZQP1(JL,JK)/(RCPD+RCPD*RVTMP2*ZQP15(JL,JK))**2
    ZZZ5 =1.0_JPRB/(RCPD+RCPD*RVTMP2*ZQP15(JL,JK))
    ZLFDCP (JL,JK) = RLMLT*ZZZ
    ZLFDCP5(JL,JK) = RLMLT*ZZZ5
    ZLSDCP (JL,JK) = RLSTT*ZZZ
    ZLSDCP5(JL,JK) = RLSTT*ZZZ5
    ZLVDCP (JL,JK) = RLVTT*ZZZ
    ZLVDCP5(JL,JK) = RLVTT*ZZZ5
    LLFLAG(JL)=.TRUE.
  ENDDO
ENDDO


!     ------------------------------------------------------------------

!*         2.2    INITIALIZATION OF CLOUD AND PRECIPITATION ARRAYS
!                 ------------------------------------------------

!       Clear cloud and freezing arrays

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    PCLC  (JL,JK) = 0.0_JPRB
    PCLC5 (JL,JK) = 0.0_JPRB
    ZQC   (JL,JK) = 0.0_JPRB
    ZQC5  (JL,JK) = 0.0_JPRB
    ZQLWC (JL,JK) = 0.0_JPRB
    ZQLWC5(JL,JK) = 0.0_JPRB
    ZQIWC (JL,JK) = 0.0_JPRB
    ZQIWC5(JL,JK) = 0.0_JPRB
    ZRFREEZE (JL,JK)=0.0_JPRB
    ZRFREEZE5(JL,JK)=0.0_JPRB
    ZCONDL (JL,JK) = 0.0_JPRB
    ZCONDL5(JL,JK) = 0.0_JPRB
    ZCONDI (JL,JK) = 0.0_JPRB
    ZCONDI5(JL,JK) = 0.0_JPRB
    ZEVAPR (JL,JK) = 0.0_JPRB
    ZEVAPR5(JL,JK) = 0.0_JPRB
    ZEVAPS (JL,JK) = 0.0_JPRB
    ZEVAPS5(JL,JK) = 0.0_JPRB
    PCOVPTOT5(JL,JK) = 0.0_JPRB
    PCOVPTOT (JL,JK) = 0.0_JPRB
  ENDDO
ENDDO

!       Set to zero precipitation fluxes at the top

DO JL=KIDIA,KFDIA
  ZRFL (JL) = 0.0_JPRB
  ZRFL5(JL) = 0.0_JPRB
  ZSFL (JL) = 0.0_JPRB
  ZSFL5(JL) = 0.0_JPRB
  PFPLSL (JL,1) = 0.0_JPRB
  PFPLSL5(JL,1) = 0.0_JPRB
  PFPLSN (JL,1) = 0.0_JPRB
  PFPLSN5(JL,1) = 0.0_JPRB
  ZCOVPTOT (JL) = 0.0_JPRB
  ZCOVPTOT5(JL) = 0.0_JPRB
  ZCOVPCLR (JL) = 0.0_JPRB
  ZCOVPCLR5(JL) = 0.0_JPRB
ENDDO

! Eta value at tropopause
DO JL=KIDIA,KFDIA
  ZTRPAUS(JL)=0.1_JPRB
ENDDO
DO JK=1,KLEV-1
  DO JL=KIDIA,KFDIA
    LLO1=CETA(JK) > 0.1_JPRB .AND. CETA(JK) < 0.4_JPRB .AND. &
       & ZTP15(JL,JK) > ZTP15(JL,JK+1)
    IF(LLO1) THEN
      ZTRPAUS(JL)=CETA(JK)
    ENDIF
  ENDDO
ENDDO

!     ------------------------------------------------------------------

!*        3. COMPUTE LAYER CLOUD AMOUNTS
!            ---------------------------

! Large loop over KLEV 
! Calculates 
!   1. diagnostic CC and QL
!   2. Convective CC and QL
!   3. Rainfall 

DO JK=KTDIA,KLEV

!       3.1   INITIALIZATION

  DO JL=KIDIA,KFDIA

!-----------------------------------
! calculate dqs/dT correction factor
!-----------------------------------

    ZOEALFAW  = 0.545_JPRB*0.17_JPRB*ZTP1(JL,JK) &
     & / COSH(0.17_JPRB*(ZTP15(JL,JK)-RLPTRC))**2  
    ZOEALFAW5 = 0.545_JPRB*(TANH(0.17_JPRB*(ZTP15(JL,JK)-RLPTRC))+1.0_JPRB)
    IF (ZTP15(JL,JK) < RTT) THEN
      ZFWAT (JL) = ZOEALFAW
      ZFWAT5(JL) = ZOEALFAW5
      Z3ES=R3IES
      Z4ES=R4IES
    ELSE
      ZFWAT (JL) = 0.0_JPRB
      ZFWAT5(JL) = 1.0_JPRB
      Z3ES=R3LES
      Z4ES=R4LES
    ENDIF
    ZFOEEW5(JL) = R2ES*EXP(Z3ES*(ZTP15(JL,JK)-RTT)/(ZTP15(JL,JK)-Z4ES))
    ZFOEEW (JL) = Z3ES*(RTT-Z4ES)*ZTP1(JL,JK)*ZFOEEW5(JL) &
              & / (ZTP15(JL,JK)-Z4ES)**2  
    ZESDP  = ZFOEEW (JL)/PAPP15(JL,JK) - PAPP1(JL,JK) &
         & * ZFOEEW5(JL)/PAPP15(JL,JK)**2  
    ZESDP5 = ZFOEEW5(JL)/PAPP15(JL,JK)
    IF (ZESDP5 > ZQMAX) THEN
      ZESDP  = 0.0_JPRB
      ZESDP5 = ZQMAX
    ENDIF

    ZFACW  = -2.0_JPRB*R5LES*ZTP1(JL,JK)/(ZTP15(JL,JK)-R4LES)**3
    ZFACW5 = R5LES/((ZTP15(JL,JK)-R4LES)**2)
    ZFACI  = -2.0_JPRB*R5IES*ZTP1(JL,JK)/(ZTP15(JL,JK)-R4IES)**3
    ZFACI5 = R5IES/((ZTP15(JL,JK)-R4IES)**2)
    ZFAC  = ZFWAT5(JL)*ZFACW+ZFACW5*ZFWAT (JL)+(1.0_JPRB-ZFWAT5(JL))*ZFACI &
     & - ZFACI5*ZFWAT (JL)  
    ZFAC5 = ZFWAT5(JL)*ZFACW5+(1.0_JPRB-ZFWAT5(JL))*ZFACI5
    ZCOR  = RETV*ZESDP/(1.0_JPRB-RETV*ZESDP5)**2
    ZCOR5 = 1.0_JPRB/(1.0_JPRB-RETV*ZESDP5)
    ZDQSDTEMP (JL) = ZFAC5*ZCOR5*PQS(JL,JK)+ZFAC5*PQS5(JL,JK)*ZCOR &
     & + ZCOR5*PQS5(JL,JK)*ZFAC  
    ZDQSDTEMP5(JL) = ZFAC5*ZCOR5*PQS5(JL,JK)
    ZCORQS (JL) = ZCONS3*ZDQSDTEMP(JL)
    ZCORQS5(JL) = 1.0_JPRB+ZCONS3*ZDQSDTEMP5(JL)

! use clipped state

    IF (ZQP15(JL,JK)>PQS5(JL,JK)) THEN
      ZQLIM (JL) = PQS (JL,JK)
      ZQLIM5(JL) = PQS5(JL,JK)
    ELSE
      ZQLIM (JL) = ZQP1(JL,JK)
      ZQLIM5(JL) = ZQP15(JL,JK)
    ENDIF

! set up critical value of humidity

    ZETA3=ZTRPAUS(JL)
    ZRH1=1.0_JPRB
    ZRH2=0.35_JPRB+0.14_JPRB*((ZETA3-0.25_JPRB)/0.15_JPRB)**2&
      & +0.04_JPRB*MIN(ZETA3-0.25_JPRB,0.0_JPRB)/0.15_JPRB
    ZRH3=1.0_JPRB
    ZDETA2=0.3_JPRB
    ZDETA1=0.09_JPRB+0.16_JPRB*(0.4_JPRB-ZETA3)/0.3_JPRB
    IF (CETA(JK) < ZETA3) THEN 
      ZCRH2=ZRH3
    ELSE IF (CETA(JK) >= ZETA3 .AND. CETA(JK) < (ZETA3+ZDETA2)) THEN 
      ZCRH2=ZRH3+(ZRH2-ZRH3)*((CETA(JK)-ZETA3)/ZDETA2)
    ELSE IF (CETA(JK) >= (ZETA3+ZDETA2) .AND. CETA(JK) < (1.0_JPRB-ZDETA1)) THEN 
      ZCRH2=ZRH2
    ELSE IF (CETA(JK) >= (1.0_JPRB-ZDETA1)) THEN 
      ZCRH2=ZRH1+(ZRH2-ZRH1)*((1.0_JPRB-CETA(JK))/ZDETA1)**0.5_JPRB
    ENDIF
! Allow ice supersaturation at cold temperatures
    IF (ZTP15(JL,JK) < RTICE) THEN
      ZSUPSAT5 = 1.8_JPRB - 3.E-03_JPRB*ZTP15(JL,JK)
      ZSUPSAT  =          - 3.E-03_JPRB*ZTP1 (JL,JK)
    ELSE
      ZSUPSAT5 = 1.0_JPRB
      ZSUPSAT  = 0.0_JPRB
    ENDIF
    ZQSAT5(JL)=PQS5(JL,JK)*ZSUPSAT5
    ZQSAT (JL)=PQS (JL,JK)*ZSUPSAT5 + PQS5(JL,JK)*ZSUPSAT

    ZQCRIT5(JL)=ZCRH2*ZQSAT5(JL)
    ZQCRIT (JL)=ZCRH2*ZQSAT (JL)
  ENDDO

! Simple UNIFORM distribution of total water from Letreut & Li (90)

  DO JL=KIDIA,KFDIA
    ZQT =ZQP1 (JL,JK)+ZL (JL,JK)+ZI (JL,JK)
    ZQT5=ZQP15(JL,JK)+ZL5(JL,JK)+ZI5(JL,JK)
    IF (ZQT5 <= ZQCRIT5(JL)) THEN
      PCLC (JL,JK) = 0.0_JPRB
      PCLC5(JL,JK) = 0.0_JPRB
      ZQC (JL,JK) = 0.0_JPRB
      ZQC5(JL,JK) = 0.0_JPRB
    ELSEIF (ZQT5 >= ZQSAT5(JL)) THEN
      PCLC (JL,JK) = 0.0_JPRB
      PCLC5(JL,JK) = 1.0_JPRB
      ZQC (JL,JK) = (1.0_JPRB-ZSCALM(JK))*(ZQSAT (JL)-ZQCRIT (JL))
      ZQC5(JL,JK) = (1.0_JPRB-ZSCALM(JK))*(ZQSAT5(JL)-ZQCRIT5(JL))
    ELSE
      ZQPD  = ZQSAT (JL)-ZQT
      ZQPD5 = ZQSAT5(JL)-ZQT5
      ZQCD  = ZQSAT (JL)-ZQCRIT (JL)
      ZQCD5 = ZQSAT5(JL)-ZQCRIT5(JL)

      ZSQRT5 = SQRT(ZQPD5/(ZQCD5-ZSCALM(JK)*(ZQT5-ZQCRIT5(JL))))  
      PCLC5(JL,JK) = 1.0_JPRB-ZSQRT5
      PCLC (JL,JK) = -(0.5_JPRB/ZSQRT5) &
       & * (ZQPD *(ZQCD5-ZSCALM(JK)*(ZQT5-ZQCRIT5(JL))) &
       & -  ZQPD5*(ZQCD -ZSCALM(JK)*(ZQT -ZQCRIT (JL)))) &
       & / (ZQCD5-ZSCALM(JK)*(ZQT5-ZQCRIT5(JL)))**2  

! Regularization of cloud fraction perturbation
      IF (LREGCL) THEN
        ZRAT=ZQPD5/ZQCD5
        ZYYY = MIN(0.3_JPRB,3.5_JPRB*SQRT(ZRAT*(1.0_JPRB-ZSCALM(JK)*(1.0_JPRB-ZRAT))**3) &
           & /(1.0_JPRB-ZSCALM(JK)))
        PCLC (JL,JK) = ZYYY * PCLC (JL,JK) 
      ENDIF
! end of regularization
     
      ZQC (JL,JK) = (ZSCALM(JK)*ZQPD+(1.0_JPRB-ZSCALM(JK))*ZQCD) &
       & * PCLC5(JL,JK)**2 &
       & + (ZSCALM(JK)*ZQPD5+(1.0_JPRB-ZSCALM(JK))*ZQCD5) &
       & * 2.0_JPRB*PCLC5(JL,JK)*PCLC(JL,JK)  
      ZQC5(JL,JK) = (ZSCALM(JK)*ZQPD5+(1.0_JPRB-ZSCALM(JK))*ZQCD5) &
       & * PCLC5(JL,JK)**2  
    ENDIF
  ENDDO

! Add convective component

  DO JL=KIDIA,KFDIA
    ZGDP (JL) = - RG*(PAPHP1(JL,JK+1)-PAPHP1(JL,JK)) &
     & / (PAPHP15(JL,JK+1)-PAPHP15(JL,JK))**2  
    ZGDP5(JL) = RG/(PAPHP15(JL,JK+1)-PAPHP15(JL,JK))

    ZLUDE (JL,JK) = PTSPHY*ZGDP5(JL)*PLUDE(JL,JK) &
     & + PTSPHY*PLUDE5(JL,JK)*ZGDP(JL)  
    ZLUDE5(JL,JK) = PLUDE5(JL,JK)*PTSPHY*ZGDP5(JL)

    IF (JK<KLEV) THEN
      LLO1=ZLUDE5(JL,JK)>=RLMIN.AND.PLU5(JL,JK+1)>=ZEPS2
    ELSE
      LLO1=.FALSE.
    ENDIF

    IF (LLO1) THEN
      PCLC (JL,JK) = PCLC(JL,JK) &
       & - PCLC(JL,JK)*(1.0_JPRB-EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1))) &
       & + ((1.0_JPRB-PCLC5(JL,JK))/PLU5(JL,JK+1)) &
       & * EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1))*ZLUDE(JL,JK) &
       & - ((1.0_JPRB-PCLC5(JL,JK))*ZLUDE5(JL,JK)/PLU5(JL,JK+1)**2) &
       & * EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1))*PLU(JL,JK+1)  
      PCLC5(JL,JK) = PCLC5(JL,JK) &
       & + (1.0_JPRB-PCLC5(JL,JK)) &
       & * (1.0_JPRB-EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1)))
  
      ZQC (JL,JK) = ZQC (JL,JK)+ZLUDE (JL,JK)
      ZQC5(JL,JK) = ZQC5(JL,JK)+ZLUDE5(JL,JK)
    ENDIF
  ENDDO

! Add compensating subsidence component

  DO JL=KIDIA,KFDIA
    ZFAC1=1.0_JPRB/(RD*ZTP15(JL,JK))
    ZRHO  = (PAPP1 (JL,JK) - ZTP1 (JL,JK)*PAPP15(JL,JK)/ZTP15(JL,JK))*ZFAC1
    ZRHO5 = PAPP15(JL,JK)*ZFAC1

    ZFAC2=1.0_JPRB/(PAPP15(JL,JK)-RETV*ZFOEEW5(JL))
    ZRODQSDP = (-ZRHO *PQS5(JL,JK) - ZRHO5*PQS (JL,JK) + ZRHO5*PQS5(JL,JK) &
           & *(PAPP1 (JL,JK)-RETV*ZFOEEW (JL))*ZFAC2)*ZFAC2
    ZRODQSDP5 = -ZRHO5*PQS5(JL,JK)*ZFAC2

    ZLDCP  = ZFWAT (JL)*ZLVDCP5(JL,JK) + ZFWAT5(JL)*ZLVDCP (JL,JK) &
         & + (1.0_JPRB-ZFWAT5(JL))*ZLSDCP (JL,JK) - ZFWAT (JL)*ZLSDCP5(JL,JK)
    ZLDCP5 = ZFWAT5(JL)*ZLVDCP5(JL,JK) + (1.0_JPRB-ZFWAT5(JL))*ZLSDCP5(JL,JK)

    ZFAC3=1.0_JPRB/(1.0_JPRB + ZLDCP5*ZDQSDTEMP5(JL))
    DTDZMO5 = RG*(1.0_JPRB/RCPD - ZLDCP5*ZRODQSDP5)*ZFAC3
    DTDZMO  = -(RG*(ZLDCP *ZRODQSDP5 + ZLDCP5*ZRODQSDP )&
          & + DTDZMO5*(ZLDCP5*ZDQSDTEMP (JL) + ZLDCP *ZDQSDTEMP5(JL)))*ZFAC3

    ZDQSDZ  = ZDQSDTEMP5(JL)*DTDZMO  + ZDQSDTEMP (JL)*DTDZMO5 - RG*ZRODQSDP
    ZDQSDZ5 = ZDQSDTEMP5(JL)*DTDZMO5 - RG*ZRODQSDP5

    ZFAC4=1.0_JPRB/ZRHO5
    LLO3=(ZDQSDZ5*(PMFU5(JL,JK)+PMFD5(JL,JK))*PTSPHY*ZFAC4 < ZQC5(JL,JK))
    IF (LLO3) THEN
      ZDQC5 = ZDQSDZ5*(PMFU5(JL,JK)+PMFD5(JL,JK))*PTSPHY*ZFAC4
      ZDQC  = ( PTSPHY*(ZDQSDZ *(PMFU5(JL,JK)+PMFD5(JL,JK))&
          & +           ZDQSDZ5*(PMFU (JL,JK)+PMFD (JL,JK)))&
          & -   ZDQC5*ZRHO )*ZFAC4
! Regularization
      IF (LREGCL) ZDQC = ZDQC * 0.1_JPRB
    ELSE
      ZDQC5 = ZQC5(JL,JK)
      ZDQC  = ZQC (JL,JK)
    ENDIF

    ZQC (JL,JK) = ZQC (JL,JK) - ZDQC
    ZQC5(JL,JK) = ZQC5(JL,JK) - ZDQC5
  ENDDO

! New cloud liquid/ice contents and condensation rates (liquid/ice)

  DO JL=KIDIA,KFDIA
    ZQLWC (JL,JK)=ZQC (JL,JK)*ZFWAT5(JL)+ZQC5(JL,JK)*ZFWAT (JL)
    ZQLWC5(JL,JK)=ZQC5(JL,JK)*ZFWAT5(JL)

    ZQIWC (JL,JK)=ZQC (JL,JK)*(1.0_JPRB-ZFWAT5(JL))-ZQC5(JL,JK)*ZFWAT (JL)
    ZQIWC5(JL,JK)=ZQC5(JL,JK)*(1.0_JPRB-ZFWAT5(JL))

    ZCONDL (JL,JK)= (ZQLWC (JL,JK)-ZL (JL,JK))*ZQTMST
    ZCONDL5(JL,JK)= (ZQLWC5(JL,JK)-ZL5(JL,JK))*ZQTMST

    ZCONDI (JL,JK)= (ZQIWC (JL,JK)-ZI (JL,JK))*ZQTMST
    ZCONDI5(JL,JK)= (ZQIWC5(JL,JK)-ZI5(JL,JK))*ZQTMST
  ENDDO

! Calculate precipitation overlap. 
! Simple form based on Maximum Overlap.

  DO JL=KIDIA,KFDIA  
    IF (PCLC5(JL,JK) > ZCOVPTOT5(JL)) THEN ! total rain frac
      ZCOVPTOT (JL) = PCLC (JL,JK)
      ZCOVPTOT5(JL) = PCLC5(JL,JK)
    ENDIF
    ZCOVPCLR (JL) = ZCOVPTOT(JL)-PCLC(JL,JK)
    ZCOVPCLR5(JL) = ZCOVPTOT5(JL)-PCLC5(JL,JK)
    IF (ZCOVPCLR5(JL) < 0.0_JPRB) THEN
      ZCOVPCLR (JL) = 0.0_JPRB
      ZCOVPCLR5(JL) = 0.0_JPRB
    ENDIF
  ENDDO

!*         3.3    CALCULATE PRECIPITATION

!    Melting of incoming snow

  DO JL=KIDIA,KFDIA
    IF (ZSFL5(JL) /= 0.0_JPRB) THEN
      ZCONS  = ZCONS2*(ZDP(JL,JK)*ZLFDCP5(JL,JK) &
       & - ZDP5(JL,JK)*ZLFDCP(JL,JK))/ZLFDCP5(JL,JK)**2  
      ZCONS5 = ZCONS2*ZDP5(JL,JK)/ZLFDCP5(JL,JK)
      IF ((ZTP15(JL,JK)-ZMELTP2) > 0.0_JPRB) THEN
        ZZ2S  = ZCONS5*ZTP1 (JL,JK)+ZCONS*(ZTP15(JL,JK)-ZMELTP2)
        ZZ2S5 = ZCONS5*(ZTP15(JL,JK)-ZMELTP2)
      ELSE
        ZZ2S  = 0.0_JPRB
        ZZ2S5 = 0.0_JPRB
      ENDIF
      IF (ZSFL5(JL) <= ZZ2S5) THEN
        ZSNMLT  = ZSFL (JL)
        ZSNMLT5 = ZSFL5(JL)
      ELSE
        ZSNMLT  = ZZ2S
        ZSNMLT5 = ZZ2S5
      ENDIF
      ZRFLN (JL) = ZRFL (JL)+ZSNMLT
      ZRFLN5(JL) = ZRFL5(JL)+ZSNMLT5
      ZSFLN (JL) = ZSFL (JL)-ZSNMLT
      ZSFLN5(JL) = ZSFL5(JL)-ZSNMLT5
      ZTP1 (JL,JK) = ZTP1 (JL,JK)-(ZSNMLT*ZCONS5-ZCONS*ZSNMLT5)/ZCONS5**2
      ZTP15(JL,JK) = ZTP15(JL,JK)-ZSNMLT5/ZCONS5
    ELSE
      ZRFLN (JL) = ZRFL (JL)
      ZRFLN5(JL) = ZRFL5(JL)
      ZSFLN (JL) = ZSFL (JL)
      ZSFLN5(JL) = ZSFL5(JL)
    ENDIF
  ENDDO

!    Diagnostic calculation of rain production from cloud liquid water

  DO JL=KIDIA,KFDIA
    IF (PCLC5(JL,JK) > ZEPS2) THEN
      IF (LEVAPLS2 .OR. LDRAIN1D) THEN
        ZLCRIT = 1.9_JPRB*RCLCRIT
      ELSE
        ZLCRIT = RCLCRIT*2._JPRB
      ENDIF
      ZCLDL  = ZQLWC (JL,JK)/PCLC5(JL,JK) &
           & - ZQLWC5(JL,JK)*PCLC (JL,JK)/PCLC5(JL,JK)**2  
      ZCLDL5 = ZQLWC5(JL,JK)/PCLC5(JL,JK)                ! in-cloud liquid 

      ZEXP35 = EXP(-(ZCLDL5/ZLCRIT)**2)
      ZD5 = ZCKCODTL*(1.0_JPRB-ZEXP35)
      ZEXPDL5 = EXP(-ZD5)

!    Regularization of autoconversion
      IF (LREGCL) THEN
        ZD  = (2.0_JPRB*ZCKCODTLA/ZLCRIT**2)*EXP(-(ZCLDL5/ZLCRIT)**2) &
         & * ZCLDL5*ZCLDL  
      ELSE
        ZD  = (2.0_JPRB*ZCKCODTL/ZLCRIT**2)*EXP(-(ZCLDL5/ZLCRIT)**2) &
         & * ZCLDL5*ZCLDL  
      ENDIF
!    end of regularization

      ZLNEW  = ZCLDL5*ZEXPDL5*PCLC(JL,JK) &
           & + PCLC5(JL,JK)*ZEXPDL5*ZCLDL &
           & - PCLC5(JL,JK)*ZCLDL5*ZEXPDL5*ZD  
      ZLNEW5 = PCLC5(JL,JK)*ZCLDL5*ZEXPDL5
      ZPRR  = ZQLWC (JL,JK)-ZLNEW
      ZPRR5 = ZQLWC5(JL,JK)-ZLNEW5
      ZQLWC (JL,JK) = ZQLWC (JL,JK)-ZPRR
      ZQLWC5(JL,JK) = ZQLWC5(JL,JK)-ZPRR5
    ELSE
      ZPRR  = 0.0_JPRB
      ZPRR5 = 0.0_JPRB
    ENDIF

!    Diagnostic calculation of rain production from cloud ice

    IF (PCLC5(JL,JK) > ZEPS2) THEN
      IF (LEVAPLS2 .OR. LDRAIN1D) THEN
        ZLCRIT = 1.E-04_JPRB
      ELSE
        ZLCRIT = RCLCRIT*2._JPRB
      ENDIF
      ZCLDI  = ZQIWC (JL,JK)/PCLC5(JL,JK) &
           & - ZQIWC5(JL,JK)*PCLC (JL,JK)/PCLC5(JL,JK)**2  
      ZCLDI5 = ZQIWC5(JL,JK)/PCLC5(JL,JK)                ! in-cloud ice

      ZEXP15 = EXP(0.025_JPRB*(ZTP15(JL,JK)-RTT))
      ZEXP25 = EXP(-(ZCLDI5/ZLCRIT)**2)
      ZD5 = ZCKCODTI * ZEXP15 * (1.0_JPRB-ZEXP25)
      ZEXPDI5 = EXP(-ZD5)

!    Regularization of autoconversion
      IF (LREGCL) THEN
        ZD = ZCKCODTIA * ZEXP15 * (ZEXP25*(2.0_JPRB*ZCLDI5*ZCLDI /ZLCRIT**2 &
           - 0.025_JPRB*ZTP1 (JL,JK)) + 0.025_JPRB*ZTP1 (JL,JK))
      ELSE
        ZD = ZCKCODTI * ZEXP15 * (ZEXP25*(2.0_JPRB*ZCLDI5*ZCLDI /ZLCRIT**2 &
           - 0.025_JPRB*ZTP1 (JL,JK)) + 0.025_JPRB*ZTP1 (JL,JK))
      ENDIF
!    end of regularization

      ZINEW  = ZCLDI5*ZEXPDI5*PCLC(JL,JK) &
           & + PCLC5(JL,JK)*ZEXPDI5*ZCLDI &
           & - PCLC5(JL,JK)*ZCLDI5*ZEXPDI5*ZD  
      ZINEW5 = PCLC5(JL,JK)*ZCLDI5*ZEXPDI5
      ZPRS  = ZQIWC (JL,JK)-ZINEW
      ZPRS5 = ZQIWC5(JL,JK)-ZINEW5
      ZQIWC (JL,JK) = ZQIWC (JL,JK)-ZPRS
      ZQIWC5(JL,JK) = ZQIWC5(JL,JK)-ZPRS5
    ELSE
      ZPRS  = 0.0_JPRB
      ZPRS5 = 0.0_JPRB
    ENDIF

!    New precipitation

    ZDR  = ZCONS2*(ZDP5(JL,JK)*(ZPRR  +ZPRS ) &
       & +         ZDP (JL,JK)*(ZPRR5 +ZPRS5)) 
    ZDR5 = ZCONS2*ZDP5(JL,JK)*(ZPRR5+ZPRS5)

! Rain fraction (different from cloud liquid water fraction!)
    IF (ZTP15(JL,JK) < RTT) THEN
      ZRFREEZE5(JL,JK)=ZCONS2* ZDP5(JL,JK)*ZPRR5
      ZRFREEZE (JL,JK)=ZCONS2*(ZDP (JL,JK)*ZPRR5 + ZDP5(JL,JK)*ZPRR )
      ZFWATR5=0.0_JPRB
      ZFWATR =0.0_JPRB
    ELSE
      ZFWATR5=1.0_JPRB
      ZFWATR =0.0_JPRB
    ENDIF 

    ZRN  = ZFWATR5*ZDR + ZDR5*ZFWATR
    ZRN5 = ZFWATR5*ZDR5
    ZSN  = -ZDR5*ZFWATR + (1.0_JPRB-ZFWATR5)*ZDR
    ZSN5 = (1.0_JPRB-ZFWATR5)*ZDR5
    ZRFLN (JL) = ZRFLN (JL)+ZRN
    ZRFLN5(JL) = ZRFLN5(JL)+ZRN5
    ZSFLN (JL) = ZSFLN (JL)+ZSN
    ZSFLN5(JL) = ZSFLN5(JL)+ZSN5

!   Precip evaporation

    ZPRTOT  = ZRFLN (JL)+ZSFLN (JL)
    ZPRTOT5 = ZRFLN5(JL)+ZSFLN5(JL)
    LLO2=ZPRTOT5>ZEPS2 .AND. ZCOVPCLR5(JL)>ZEPS2 .AND. (LEVAPLS2 .OR. LDRAIN1D)
    IF (LLO2) THEN
      ZPRECLR  = (ZPRTOT5*ZCOVPCLR(JL)+ZCOVPCLR5(JL)*ZPRTOT) &
       & / ZCOVPTOT5(JL) &
       & - ZPRTOT5*ZCOVPCLR5(JL)*ZCOVPTOT(JL) &
       & / ZCOVPTOT5(JL)**2  
      ZPRECLR5 = ZPRTOT5*ZCOVPCLR5(JL)/ZCOVPTOT5(JL)

!     This is the humidity in the moistest zcovpclr region

      ZQE  = PQS(JL,JK) - ((PQS5(JL,JK)-ZQLIM5(JL))*ZCOVPCLR(JL) &
       & + ZCOVPCLR5(JL)*PQS(JL,JK)-ZCOVPCLR5(JL)*ZQLIM(JL)) &
       & / (1.0_JPRB-PCLC5(JL,JK))**2 &
       & - 2.0_JPRB*(PQS5(JL,JK)-ZQLIM5(JL))*ZCOVPCLR5(JL) &
       & * PCLC(JL,JK)/(1.0_JPRB-PCLC5(JL,JK))**3  
      ZQE5 = PQS5(JL,JK)-(PQS5(JL,JK)-ZQLIM5(JL))*ZCOVPCLR5(JL)&
       & / (1.0_JPRB-PCLC5(JL,JK))**2  

      ZBETA  = 0.5777_JPRB*(RG*RPECONS/5.09E-3_JPRB) &
       & * (5.09E-3_JPRB*ZCOVPCLR5(JL)/(ZPRECLR5 &
       & * SQRT(PAPP15(JL,JK)/PAPHP15(JL,KLEV+1))))**0.4223_JPRB &
       & * ((SQRT(PAPP15(JL,JK)/PAPHP15(JL,KLEV+1))*ZPRECLR &
       & + 0.5_JPRB*ZPRECLR5*PAPP1(JL,JK) &
       & / SQRT(PAPP15(JL,JK)*PAPHP15(JL,KLEV+1)) &
       & - 0.5_JPRB*ZPRECLR5  &
       & * SQRT(PAPP15(JL,JK)/PAPHP15(JL,KLEV+1)) &
       & * PAPHP1(JL,KLEV+1)/PAPHP15(JL,KLEV+1)) &
       & / ZCOVPCLR5(JL) &
       & - ZPRECLR5*SQRT(PAPP15(JL,JK)/PAPHP15(JL,KLEV+1)) &
       & * ZCOVPCLR(JL) / ZCOVPCLR5(JL)**2)  

      ZBETA5=RG*RPECONS*(SQRT(PAPP15(JL,JK) &
       & /PAPHP15(JL,KLEV+1))/5.09E-3_JPRB*ZPRECLR5 &
       & /ZCOVPCLR5(JL))**0.5777_JPRB  

!     implicit solution:

      ZB  = PTSPHY*((PQS5(JL,JK)-ZQE5)*ZBETA+ZBETA5*PQS(JL,JK) &
       & - ZBETA5*ZQE)/(1.0_JPRB+ZBETA5*PTSPHY*ZCORQS5(JL)) &
       & - (PTSPHY**2)*ZBETA5*(PQS5(JL,JK)-ZQE5) &
       & * (ZBETA5*ZCORQS(JL)+ZCORQS5(JL)*ZBETA) &
       & / (1.0_JPRB+ZBETA5*PTSPHY*ZCORQS5(JL))**2  
      ZB5 = PTSPHY*ZBETA5*(PQS5(JL,JK)-ZQE5) &
       & / (1.0_JPRB+ZBETA5*PTSPHY*ZCORQS5(JL))  

!     exact solution:

!     ZEXP5 = EXP(-ZBETA5*ZCORQS5(JL)*PTSPHY)
!     ZB  = (ZEXP5*(-ZCORQS5(JL)*(PQS(JL,JK)-ZQE) &
!      &  + (PQS5(JL,JK)-ZQE5) &
!      &  * (PTSPHY*ZCORQS5(JL)*ZBETA5*ZCORQS(JL) &
!      &  + PTSPHY*ZCORQS5(JL)*ZCORQS5(JL)*ZBETA + ZCORQS(JL))) &
!      &  + ZCORQS5(JL)*(PQS(JL,JK)-ZQE) &
!      &  - (PQS5(JL,JK)-ZQE5)*ZCORQS(JL)) &
!      &  / ZCORQS5(JL)**2
!     ZB5 =(PQS5(JL,JK)-ZQE5)*(_ONE_-ZEXP5)/ZCORQS5(JL)

      ZDTGDP (JL) = - PTSPHY*RG*(PAPHP1(JL,JK+1)-PAPHP1(JL,JK)) &
       & / (PAPHP15(JL,JK+1)-PAPHP15(JL,JK))**2  
      ZDTGDP5(JL) =PTSPHY*RG/(PAPHP15(JL,JK+1)-PAPHP15(JL,JK))
      ZDPR  = (ZCOVPCLR5(JL)*ZB+ZB5*ZCOVPCLR(JL)) &
       & / ZDTGDP5(JL) &
       & - ZCOVPCLR5(JL)*ZB5*ZDTGDP(JL)/ZDTGDP5(JL)**2  
      ZDPR5 = ZCOVPCLR5(JL)*ZB5/ZDTGDP5(JL)
      IF (ZDPR5 > ZPRECLR5) THEN
        ZDPR  = ZPRECLR
        ZDPR5 = ZPRECLR5
      ENDIF
      ZPRECLR  = ZPRECLR-ZDPR  ! take away from clr sky flux
      ZPRECLR5 = ZPRECLR5-ZDPR5
      IF (ZPRECLR5 <= 0.0_JPRB) THEN !reset
        ZCOVPTOT (JL) = PCLC(JL,JK)
        ZCOVPTOT5(JL) = PCLC5(JL,JK)
      ENDIF
      PCOVPTOT (JL,JK) = ZCOVPTOT (JL)
      PCOVPTOT5(JL,JK) = ZCOVPTOT5(JL)

! warm proportion
      ZEVAPR (JL,JK) = (ZDPR5*ZRFLN(JL)+ZRFLN5(JL)*ZDPR)/ZPRTOT5 &
                   & - ZDPR5*ZRFLN5(JL)*ZPRTOT/ZPRTOT5**2  
      ZEVAPR5(JL,JK) = ZDPR5*ZRFLN5(JL)/ZPRTOT5     
      ZRFLN (JL) = ZRFLN (JL)-ZEVAPR (JL,JK)
      ZRFLN5(JL) = ZRFLN5(JL)-ZEVAPR5(JL,JK) 
     
! ice proportion
      ZEVAPS (JL,JK) = (ZDPR5*ZSFLN(JL)+ZSFLN5(JL)*ZDPR)/ZPRTOT5 &
                   & - ZDPR5*ZSFLN5(JL)*ZPRTOT/ZPRTOT5**2  
      ZEVAPS5(JL,JK) = ZDPR5*ZSFLN5(JL)/ZPRTOT5        
      ZSFLN (JL) = ZSFLN (JL)-ZEVAPS (JL,JK)
      ZSFLN5(JL) = ZSFLN5(JL)-ZEVAPS5(JL,JK)
    ENDIF

  ENDDO

!   Incrementation of T and Q and fluxes' swap

  DO JL=KIDIA,KFDIA
    ZDQDT(JL)=-(ZCONDL (JL,JK)+ZCONDI (JL,JK)) &
            & +(PLUDE (JL,JK)+ZEVAPR (JL,JK)+ZEVAPS (JL,JK))*ZGDP5(JL) &
            & +(PLUDE5(JL,JK)+ZEVAPR5(JL,JK)+ZEVAPS5(JL,JK))*ZGDP (JL)
    ZDQDT5(JL)=-(ZCONDL5(JL,JK)+ZCONDI5(JL,JK)) &
            & +(PLUDE5(JL,JK)+ZEVAPR5(JL,JK)+ZEVAPS5(JL,JK))*ZGDP5(JL)

    ZDTDT(JL)= ZLVDCP (JL,JK)*ZCONDL5(JL,JK)+ZLSDCP (JL,JK)*ZCONDI5(JL,JK) &
            & +ZLVDCP5(JL,JK)*ZCONDL (JL,JK)+ZLSDCP5(JL,JK)*ZCONDI (JL,JK) &
            & -( ZLVDCP (JL,JK)*ZEVAPR5(JL,JK)+ZLSDCP (JL,JK)*ZEVAPS5(JL,JK) &
            & +  ZLVDCP5(JL,JK)*ZEVAPR (JL,JK)+ZLSDCP5(JL,JK)*ZEVAPS (JL,JK) &
            & +  PLUDE (JL,JK)*(ZFWAT5(JL)*ZLVDCP5(JL,JK)+ &
            &        (1.0_JPRB-ZFWAT5(JL))*ZLSDCP5(JL,JK))&
            & +  PLUDE5(JL,JK)*(ZFWAT (JL)*(ZLVDCP5(JL,JK)-ZLSDCP5(JL,JK)) &
            & +                (ZFWAT5(JL)*ZLVDCP (JL,JK)+ &
            &        (1.0_JPRB-ZFWAT5(JL))*ZLSDCP (JL,JK))) &
            &   -(ZLSDCP (JL,JK)-ZLVDCP (JL,JK))*ZRFREEZE5(JL,JK) &
            &   -(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK))*ZRFREEZE (JL,JK) )&
            & *ZGDP5(JL) &
            & -( ZLVDCP5(JL,JK)*ZEVAPR5(JL,JK)+ZLSDCP5(JL,JK)*ZEVAPS5(JL,JK) &
            & +  PLUDE5(JL,JK)*(ZFWAT5(JL)*ZLVDCP5(JL,JK)+ &
            &   (1.0_JPRB-ZFWAT5(JL))*ZLSDCP5(JL,JK))&
            &   -(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK))*ZRFREEZE5(JL,JK) )&
            & *ZGDP (JL)
    ZDTDT5(JL)= ZLVDCP5(JL,JK)*ZCONDL5(JL,JK)+ZLSDCP5(JL,JK)*ZCONDI5(JL,JK) &
            & -( ZLVDCP5(JL,JK)*ZEVAPR5(JL,JK)+ZLSDCP5(JL,JK)*ZEVAPS5(JL,JK) &
            & +PLUDE5(JL,JK)*(ZFWAT5(JL)*ZLVDCP5(JL,JK)+ &
            & (1.0_JPRB-ZFWAT5(JL))*ZLSDCP5(JL,JK))&
            & -(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK))*ZRFREEZE5(JL,JK) )&
            & *ZGDP5(JL)

! first guess T and Q
    ZTP1 (JL,JK) = ZTP1 (JL,JK) + PTSPHY*ZDTDT (JL)
    ZTP15(JL,JK) = ZTP15(JL,JK) + PTSPHY*ZDTDT5(JL)
    ZQP1 (JL,JK) = ZQP1 (JL,JK) + PTSPHY*ZDQDT (JL)
    ZQP15(JL,JK) = ZQP15(JL,JK) + PTSPHY*ZDQDT5(JL)

    ZPP (JL) = PAPP1 (JL,JK)
    ZPP5(JL) = PAPP15(JL,JK)
    ZQOLD (JL) = ZQP1 (JL,JK)
    ZQOLD5(JL) = ZQP15(JL,JK)
  ENDDO

! clipping of final qv

  IK=JK
  ICALL=0
  CALL CUADJTQSTL ( KIDIA, KFDIA, KLON, KLEV, IK,&
   & ZPP5 , ZTP15 , ZQP15,&
   & ZPP  , ZTP1  , ZQP1 , LLFLAG, ICALL  )  

  DO JL=KIDIA,KFDIA
    IF ((ZQOLD5(JL)-ZQP15(JL,JK)) >= 0.0_JPRB) THEN
      ZDQ5(JL) = ZQOLD5(JL)-ZQP15(JL,JK) 
      ZDQ (JL) = ZQOLD (JL)-ZQP1 (JL,JK)
! Regularization
      IF (LREGCL) THEN
        ZDQ (JL) = ZDQ (JL) * 0.7_JPRB
      ENDIF
    ELSE
      ZDQ (JL) = 0.0_JPRB
      ZDQ5(JL) = 0.0_JPRB
    ENDIF
    ZDR2  = ZCONS2*(ZDP5(JL,JK)*ZDQ(JL)+ZDQ5(JL)*ZDP(JL,JK))
    ZDR25 = ZCONS2*ZDP5(JL,JK)*ZDQ5(JL)

! Update rain fraction and freezing.
! Note: impact of new temperature ZTP1 on ZFWAT is neglected here.
    IF (ZTP15(JL,JK) < RTT) THEN
      ZRFREEZE25=ZFWAT5(JL)*ZDR25
      ZRFREEZE2 =ZFWAT (JL)*ZDR25 + ZFWAT5(JL)*ZDR2 
      ZFWATR5=0.0_JPRB
      ZFWATR =0.0_JPRB
    ELSE
      ZRFREEZE25=0.0_JPRB
      ZRFREEZE2 =0.0_JPRB
      ZFWATR5=1.0_JPRB
      ZFWATR =0.0_JPRB
    ENDIF 

    ZRN  = ZFWATR5*ZDR2 +ZDR25*ZFWATR
    ZRN5 = ZFWATR5*ZDR25
    ZSN  = (1.0_JPRB-ZFWATR5)*ZDR2 -ZDR25*ZFWATR
    ZSN5 = (1.0_JPRB-ZFWATR5)*ZDR25
    
! Note: The extra condensation due to the adjustment goes directly to precipitation
    ZCONDL (JL,JK)=ZCONDL (JL,JK)+(ZFWATR5*ZDQ (JL)+ZFWATR *ZDQ5(JL))*ZQTMST
    ZCONDL5(JL,JK)=ZCONDL5(JL,JK)+ ZFWATR5*ZDQ5(JL)*ZQTMST

    ZCONDI (JL,JK)=ZCONDI (JL,JK)+((1.0_JPRB-ZFWATR5)*ZDQ (JL)-ZFWATR *ZDQ5(JL))*ZQTMST
    ZCONDI5(JL,JK)=ZCONDI5(JL,JK)+ (1.0_JPRB-ZFWATR5)*ZDQ5(JL)*ZQTMST

    ZRFLN (JL) = ZRFLN (JL) + ZRN
    ZRFLN5(JL) = ZRFLN5(JL) + ZRN5
    ZSFLN (JL) = ZSFLN (JL) + ZSN
    ZSFLN5(JL) = ZSFLN5(JL) + ZSN5
    ZRFREEZE5(JL,JK)=ZRFREEZE5(JL,JK)+ZRFREEZE25
    ZRFREEZE (JL,JK)=ZRFREEZE (JL,JK)+ZRFREEZE2
  ENDDO

  DO JL=KIDIA,KFDIA
    ZDQDT(JL)=-(ZCONDL (JL,JK)+ZCONDI (JL,JK)) &
            & +(PLUDE (JL,JK)+ZEVAPR (JL,JK)+ZEVAPS (JL,JK))*ZGDP5(JL) &
            & +(PLUDE5(JL,JK)+ZEVAPR5(JL,JK)+ZEVAPS5(JL,JK))*ZGDP (JL)
    ZDQDT5(JL)=-(ZCONDL5(JL,JK)+ZCONDI5(JL,JK)) &
            & +(PLUDE5(JL,JK)+ZEVAPR5(JL,JK)+ZEVAPS5(JL,JK))*ZGDP5(JL)

    ZDTDT(JL)= ZLVDCP (JL,JK)*ZCONDL5(JL,JK)+ZLSDCP (JL,JK)*ZCONDI5(JL,JK) &
            & +ZLVDCP5(JL,JK)*ZCONDL (JL,JK)+ZLSDCP5(JL,JK)*ZCONDI (JL,JK) &
            & -( ZLVDCP (JL,JK)*ZEVAPR5(JL,JK)+ZLSDCP (JL,JK)*ZEVAPS5(JL,JK) &
            & +  ZLVDCP5(JL,JK)*ZEVAPR (JL,JK)+ZLSDCP5(JL,JK)*ZEVAPS (JL,JK) &
            & +  PLUDE (JL,JK)*(ZFWAT5(JL)*ZLVDCP5(JL,JK)+ &
            &        (1.0_JPRB-ZFWAT5(JL))*ZLSDCP5(JL,JK))&
            & +  PLUDE5(JL,JK)*(ZFWAT (JL)*(ZLVDCP5(JL,JK)-ZLSDCP5(JL,JK)) &
            & +                (ZFWAT5(JL)*ZLVDCP (JL,JK)+ &
            &        (1.0_JPRB-ZFWAT5(JL))*ZLSDCP (JL,JK))) &
            &   -(ZLSDCP (JL,JK)-ZLVDCP (JL,JK))*ZRFREEZE5(JL,JK) &
            &   -(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK))*ZRFREEZE (JL,JK) )&
            & *ZGDP5(JL) &
            & -( ZLVDCP5(JL,JK)*ZEVAPR5(JL,JK)+ZLSDCP5(JL,JK)*ZEVAPS5(JL,JK) &
            & +  PLUDE5(JL,JK)*(ZFWAT5(JL)*ZLVDCP5(JL,JK)+ &
            &   (1.0_JPRB-ZFWAT5(JL))*ZLSDCP5(JL,JK))&
            &   -(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK))*ZRFREEZE5(JL,JK) )&
            & *ZGDP (JL)
    ZDTDT5(JL)= ZLVDCP5(JL,JK)*ZCONDL5(JL,JK)+ZLSDCP5(JL,JK)*ZCONDI5(JL,JK) &
            & -( ZLVDCP5(JL,JK)*ZEVAPR5(JL,JK)+ZLSDCP5(JL,JK)*ZEVAPS5(JL,JK) &
            & +PLUDE5(JL,JK)*(ZFWAT5(JL)*ZLVDCP5(JL,JK)+ &
            & (1.0_JPRB-ZFWAT5(JL))*ZLSDCP5(JL,JK))&
            & -(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK))*ZRFREEZE5(JL,JK) )&
            & *ZGDP5(JL)

    ZDLDT (JL)=(ZQLWC (JL,JK)-ZL (JL,JK))*ZQTMST
    ZDLDT5(JL)=(ZQLWC5(JL,JK)-ZL5(JL,JK))*ZQTMST

    ZDIDT (JL)=(ZQIWC (JL,JK)-ZI (JL,JK))*ZQTMST
    ZDIDT5(JL)=(ZQIWC5(JL,JK)-ZI5(JL,JK))*ZQTMST

    PTENQ (JL,JK) = ZDQDT (JL)
    PTENQ5(JL,JK) = ZDQDT5(JL)
    PTENT (JL,JK) = ZDTDT (JL) 
    PTENT5(JL,JK) = ZDTDT5(JL)
    PTENL (JL,JK) = ZDLDT (JL)
    PTENL5(JL,JK) = ZDLDT5(JL)
    PTENI (JL,JK) = ZDIDT (JL) 
    PTENI5(JL,JK) = ZDIDT5(JL)

    PFPLSL (JL,JK+1) = ZRFLN (JL)
    PFPLSL5(JL,JK+1) = ZRFLN5(JL)
    PFPLSN (JL,JK+1) = ZSFLN (JL)
    PFPLSN5(JL,JK+1) = ZSFLN5(JL)
  ENDDO

  DO JL=KIDIA,KFDIA
    ZRFL (JL) = ZRFLN (JL)
    ZRFL5(JL) = ZRFLN5(JL)
    ZSFL (JL) = ZSFLN (JL)
    ZSFL5(JL) = ZSFLN5(JL)
  ENDDO

ENDDO  !jk

!*     ENTHALPY FLUXES DUE TO PRECIPITATION
!      ------------------------------------

DO JK=1,KLEV+1
  DO JL=KIDIA,KFDIA
    PFHPSL (JL,JK) = -PFPLSL (JL,JK)*RLVTT
    PFHPSL5(JL,JK) = -PFPLSL5(JL,JK)*RLVTT
    PFHPSN (JL,JK) = -PFPLSN (JL,JK)*RLSTT
    PFHPSN5(JL,JK) = -PFPLSN5(JL,JK)*RLSTT
  ENDDO
ENDDO

!     ------------------------------------------------------------------

END ASSOCIATE
!IF (LHOOK) CALL DR_HOOK('CLOUDSC2TL',1,ZHOOK_HANDLE)
END SUBROUTINE CLOUDSC2TL

