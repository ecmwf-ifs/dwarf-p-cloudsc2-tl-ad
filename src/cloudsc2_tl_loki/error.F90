MODULE ERROR_MOD
  USE PARKIND1, ONLY: JPIM, JPRB
  IMPLICIT NONE

CONTAINS

  SUBROUTINE ERROR_NORM(NLON, FIELD, PERT5, PERT, ZNORM, ZCOUNT, ZLAMBDA)
    INTEGER(KIND=JPIM), INTENT(IN) :: NLON
    REAL(KIND=JPRB), INTENT(IN) :: FIELD(:,:), PERT5(:,:), PERT(:,:)
    REAL(KIND=JPRB), INTENT(IN) :: ZLAMBDA
    REAL(KIND=JPRB), INTENT(INOUT) :: ZNORM, ZCOUNT

    IF (ABS(SUM(PERT(1:NLON,:)*ZLAMBDA)) > EPSILON(ZLAMBDA) ) THEN
      ZCOUNT = ZCOUNT + 1._JPRB
      ZNORM = ZNORM + ABS(SUM(FIELD(1:NLON,:)-PERT5(1:NLON,:)) / SUM(PERT(1:NLON,:)*ZLAMBDA))
    END IF
  END SUBROUTINE ERROR_NORM


  SUBROUTINE VALIDATE_TAYLOR_TEST(NPROMA, NLEV, NLAM, NGPTOT, &
   & FIELD_T, PERT5_T, PERT_T, &
   & FIELD_Q, PERT5_Q, PERT_Q, &
   & FIELD_L, PERT5_L, PERT_L, &
   & FIELD_I, PERT5_I, PERT_I, &
   & FIELD_A, PERT5_A, PERT_A, &
   & PFPLSL, PERT5_FPLSL, PERT_FPLSL, &
   & PFPLSN, PERT5_FPLSN, PERT_FPLSN, &
   & PFHPSL, PERT5_FHPSL, PERT_FHPSL, &
   & PFHPSN, PERT5_FHPSN, PERT_FHPSN, &
   & PCOVPTOT, PERT5_COVPTOT, PERT_COVPTOT &
   & )
    INTEGER(KIND=JPIM), INTENT(IN)    :: NPROMA, NLEV, NLAM, NGPTOT
    REAL(KIND=JPRB), INTENT(IN) :: FIELD_T(:,:,:), PERT5_T(:,:,:,:), PERT_T(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: FIELD_Q(:,:,:), PERT5_Q(:,:,:,:), PERT_Q(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: FIELD_L(:,:,:), PERT5_L(:,:,:,:), PERT_L(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: FIELD_I(:,:,:), PERT5_I(:,:,:,:), PERT_I(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: FIELD_A(:,:,:), PERT5_A(:,:,:,:), PERT_A(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: PFPLSL(:,:,:), PERT5_FPLSL(:,:,:,:), PERT_FPLSL(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: PFPLSN(:,:,:), PERT5_FPLSN(:,:,:,:), PERT_FPLSN(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: PFHPSL(:,:,:), PERT5_FHPSL(:,:,:,:), PERT_FHPSL(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: PFHPSN(:,:,:), PERT5_FHPSN(:,:,:,:), PERT_FHPSN(:,:,:)
    REAL(KIND=JPRB), INTENT(IN) :: PCOVPTOT(:,:,:), PERT5_COVPTOT(:,:,:,:), PERT_COVPTOT(:,:,:)

    INTEGER(KIND=JPIM) :: JKGLO, IBL, ICEND, ILAM, NGPBLKS
    INTEGER(KIND=JPIM) :: ISTART,ITEST,INEGAT,ITEMPNEGAT
    REAL(KIND=JPRB)    :: ZLAMBDA, ZCOUNT, ZNORM, ZNORMG(10)

    NGPBLKS = (NGPTOT / NPROMA) + MIN(MOD(NGPTOT,NPROMA), 1)
    DO JKGLO=1,NGPTOT,NPROMA
      IBL = (JKGLO-1)/NPROMA+1
      ICEND = MIN(NPROMA,NGPTOT-JKGLO+1)

      ! Loop over incrementing states
      DO ILAM=1,10
        ZLAMBDA = 10._JPRB**(-REAL(ILAM,JPRB))

        ! Compute final test norm
        ZCOUNT = 0._JPRB
        ZNORM  = 0._JPRB
        CALL ERROR_NORM(ICEND, FIELD_T(:,:,IBL),  PERT5_T(:,:,ILAM,IBL),  PERT_T(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, FIELD_Q(:,:,IBL),  PERT5_Q(:,:,ILAM,IBL),  PERT_Q(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, FIELD_L(:,:,IBL),  PERT5_L(:,:,ILAM,IBL),  PERT_L(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, FIELD_I(:,:,IBL),  PERT5_I(:,:,ILAM,IBL),  PERT_I(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, FIELD_A(:,:,IBL),  PERT5_A(:,:,ILAM,IBL),  PERT_A(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, PFPLSL(:,:,IBL),   PERT5_FPLSL(:,:,ILAM,IBL),   PERT_FPLSL(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, PFPLSN(:,:,IBL),   PERT5_FPLSN(:,:,ILAM,IBL),   PERT_FPLSN(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, PFHPSL(:,:,IBL),   PERT5_FHPSL(:,:,ILAM,IBL),   PERT_FHPSL(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, PFHPSN(:,:,IBL),   PERT5_FHPSN(:,:,ILAM,IBL),   PERT_FHPSN(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)
        CALL ERROR_NORM(ICEND, PCOVPTOT(:,:,IBL), PERT5_COVPTOT(:,:,ILAM,IBL), PERT_COVPTOT(:,:,IBL), ZNORM, ZCOUNT, ZLAMBDA)

        ! Global norm (normalize by number of active statistics)
        IF (ZNORM == 0._JPRB .OR. ZCOUNT == 0._JPRB) THEN
          print *, ' TL is totally wrong !!! ',ZNORM,ZCOUNT
          stop
        ELSE
          ZNORMG(ILAM)=MAX(ZNORMG(ILAM),ZNORM/ZCOUNT)
        ENDIF
      ENDDO
    ENDDO

    ! Evaluate the test and PRINT the otput
    PRINT *, ' TL Taylor test '
    PRINT *, '                Lambda   Result'
    istart=0
    DO ILAM=1,10
      PRINT *, ILAM, ZNORMG(ILAM)
      ! Redefine ZNORMG
      ZNORMG(ILAM)=ABS(1._JPRB - ZNORMG(ILAM))
      ! filter out first members with strong NL departures
      if (istart == 0 .AND.  ZNORMG(ILAM) < 0.5_JPRB ) istart=ILAM
    ENDDO

    PRINT *, '   ==============================================   '
    IF (ISTART == 0 .OR. ISTART > 4 ) THEN
      PRINT *, '       TEST FAILLED, err 13 '
    ELSE
      ! V-shape test
      ITEST=-10
      INEGAT=1
      DO ILAM=ISTART,10-1
        IF (ZNORMG(ILAM+1)/ZNORMG(ILAM) < 1._JPRB ) THEN
          ITEMPNEGAT = 1
        ELSE
          ITEMPNEGAT = 0
        ENDIF
        IF (INEGAT > ITEMPNEGAT) ITEST=ITEST+10
        INEGAT=ITEMPNEGAT
      ENDDO
      IF (ITEST == -10) ITEST = 11 ! no change of sign at all
      ! Accuracy test
      IF (MINVAL(ZNORMG(ISTART:10)) > 0.00001_JPRB) ITEST=ITEST+7  ! Hard limit
      IF (MINVAL(ZNORMG(ISTART:10)) > 0.000001_JPRB) ITEST=ITEST+5  ! Soft limit
      ! Final PRINTs
      IF (ITEST > 5) THEN
        PRINT *, '       TEST FAILLED, err ',ITEST
      ELSE
        PRINT *, '       TEST PASSED, penalty ',ITEST
      ENDIF
    ENDIF

    PRINT *, '   ==============================================   '

  END SUBROUTINE VALIDATE_TAYLOR_TEST

END MODULE ERROR_MOD
