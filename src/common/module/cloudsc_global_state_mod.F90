MODULE CLOUDSC_GLOBAL_STATE_MOD
  ! Driver module to manage the setup and teardown of the dwarf memory state
  USE PARKIND1,  ONLY : JPIM, JPRB
  USE YOMPHYDER, ONLY : STATE_TYPE
  USE YOECLDP,   ONLY : NCLV, YRECLDP, YRECLDP_LOAD_PARAMETERS
  USE YOMCST,    ONLY : YOMCST_LOAD_PARAMETERS
  USE YOETHF,    ONLY : YOETHF_LOAD_PARAMETERS
  USE YOEPHLI  , ONLY : YREPHLI, YREPHLI_LOAD_PARAMETERS

  USE FILE_IO_MOD, ONLY: INPUT_INITIALIZE, INPUT_FINALIZE, LOAD_SCALAR, LOAD_ARRAY, WRITE_ARRAY, WRITE_SCALAR
  USE EXPAND_MOD, ONLY: LOAD_AND_EXPAND, LOAD_AND_EXPAND_STATE
  USE VALIDATE_MOD, ONLY: VALIDATE
  USE CLOUDSC_MPI_MOD, ONLY: IRANK

  IMPLICIT NONE

  TYPE CLOUDSC_GLOBAL_STATE
    ! Memory state containing raw fields annd tendencies for CLOUDSC dwarf
    !
    ! Note that the global state has an additional outermost block
    ! dimension allocated for each field variable.
    INTEGER(KIND=JPIM)                   :: NPROMA, KLEV    ! Grid points and vertical levels per block
    INTEGER(KIND=JPIM)                   :: NGPTOT, NBLOCKS ! Total number of grid points and blocks
    LOGICAL                              :: LDSLPHY 
    LOGICAL                              :: LDMAINCALL      ! T if main call to cloudsc
    REAL(KIND=JPRB)                      :: PTSPHY          ! Physics timestep

    ! Input field variables and tendencies
    REAL(KIND=JPRB), ALLOCATABLE :: PT(:,:,:)       ! T at start of callpar
    REAL(KIND=JPRB), ALLOCATABLE :: PQ(:,:,:)       ! Q at start of callpar
    TYPE(STATE_TYPE), ALLOCATABLE :: TENDENCY_CML(:) ! cumulative tendency used for final output
    TYPE(STATE_TYPE), ALLOCATABLE :: TENDENCY_LOC(:) ! local tendency from cloud scheme
    REAL(KIND=JPRB), ALLOCATABLE :: PAP(:,:,:)      ! Pressure on full levels
    REAL(KIND=JPRB), ALLOCATABLE :: PAPH(:,:,:)     ! Pressure on half levels
    REAL(KIND=JPRB), ALLOCATABLE :: PLU(:,:,:)      ! Conv. condensate
    REAL(KIND=JPRB), ALLOCATABLE :: PLUDE(:,:,:)    ! Conv. detrained water 
    REAL(KIND=JPRB), ALLOCATABLE :: PMFU(:,:,:)     ! Conv. mass flux up
    REAL(KIND=JPRB), ALLOCATABLE :: PMFD(:,:,:)     ! Conv. mass flux down
    REAL(KIND=JPRB), ALLOCATABLE :: PA(:,:,:)       ! Original Cloud fraction (t)
    REAL(KIND=JPRB), ALLOCATABLE :: PEXTRA(:,:,:,:) ! extra fields
    REAL(KIND=JPRB), ALLOCATABLE :: PCLV(:,:,:,:) 
    REAL(KIND=JPRB), ALLOCATABLE :: PSUPSAT(:,:,:)

    ! Output fields used for validation
    REAL(KIND=JPRB), ALLOCATABLE :: PCOVPTOT(:,:,:) ! Precip fraction
    REAL(KIND=JPRB), ALLOCATABLE :: PRAINFRAC_TOPRFZ(:,:) 
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQLF(:,:,:)   ! Flux of liquid
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQIF(:,:,:)   ! Flux of ice
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQLNG(:,:,:)  ! -ve corr for liq
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQNNG(:,:,:)  ! -ve corr for ice
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQRF(:,:,:)   ! Flux diagnostics
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQSF(:,:,:)   ! for DDH, generic
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQRNG(:,:,:)  ! rain
    REAL(KIND=JPRB), ALLOCATABLE :: PFCQSNG(:,:,:)  ! snow
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQLTUR(:,:,:) ! liquid flux due to VDF
    REAL(KIND=JPRB), ALLOCATABLE :: PFSQITUR(:,:,:) ! ice flux due to VDF
    REAL(KIND=JPRB), ALLOCATABLE :: PFPLSL(:,:,:)   ! liq+rain sedim flux
    REAL(KIND=JPRB), ALLOCATABLE :: PFPLSN(:,:,:)   ! ice+snow sedim flux
    REAL(KIND=JPRB), ALLOCATABLE :: PFHPSL(:,:,:)   ! Enthalpy flux for liq
    REAL(KIND=JPRB), ALLOCATABLE :: PFHPSN(:,:,:)   ! Enthalp flux for ice

    ! Underlying data buffers for AOSOA allcoated STATE_TYPE arrays
    REAL(KIND=JPRB), ALLOCATABLE :: B_CML(:,:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: B_TMP(:,:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: B_LOC(:,:,:,:)
  CONTAINS
    PROCEDURE :: LOAD => CLOUDSC_GLOBAL_STATE_LOAD
    PROCEDURE :: VALIDATE => CLOUDSC_GLOBAL_STATE_VALIDATE
    PROCEDURE :: WRITE_REFERENCE => CLOUDSC_GLOBAL_STATE_WRITE_REFERENCE
  END TYPE CLOUDSC_GLOBAL_STATE

  INTERFACE FIELD_INIT
     PROCEDURE FIELD_INIT_R1, FIELD_INIT_R2, FIELD_INIT_R3, FIELD_INIT_STATE
  END INTERFACE FIELD_INIT

CONTAINS

  SUBROUTINE FIELD_INIT_R1(FIELD, NPROMA,NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(KIND=JPRB), ALLOCATABLE, INTENT(INOUT) :: FIELD(:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NBLOCKS
    INTEGER(KIND=JPIM) :: B
    ALLOCATE(FIELD(NPROMA, NBLOCKS))

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R1

  SUBROUTINE FIELD_INIT_R2(FIELD, NPROMA, NLEV, NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(KIND=JPRB), ALLOCATABLE, INTENT(INOUT) :: FIELD(:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NLEV, NBLOCKS
    INTEGER(KIND=JPIM) :: B
    ALLOCATE(FIELD(NPROMA, NLEV, NBLOCKS))

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R2

  SUBROUTINE FIELD_INIT_R3(FIELD, NPROMA, NLEV, NDIM, NBLOCKS)
    ! Allocate and initialize (zero) empty output fields
    REAL(KIND=JPRB), ALLOCATABLE, INTENT(INOUT) :: FIELD(:,:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NLEV, NDIM, NBLOCKS
    INTEGER(KIND=JPIM) :: B
    ALLOCATE(FIELD(NPROMA, NLEV, NDIM, NBLOCKS))

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       FIELD(:,:,:,B) = 0.0_JPRB
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_R3

  SUBROUTINE FIELD_INIT_STATE(STATE, BUFFER, NPROMA, NLEV, NDIM, NBLOCKS)
    ! Allocate empty state struct array
    !
    ! Note, the resulting AOSOA pattern is a pain to roll by hand in Fortran
    ! and could be done either via parameterized derived types or templating.
    TYPE(STATE_TYPE), ALLOCATABLE, INTENT(INOUT) :: STATE(:)
    REAL(KIND=JPRB), ALLOCATABLE, TARGET, INTENT(INOUT) :: BUFFER(:,:,:,:)
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NLEV, NDIM, NBLOCKS
    INTEGER(KIND=JPIM) :: B, NFIELDS

    NFIELDS = 3+NDIM
    ALLOCATE(STATE(NBLOCKS))
    ALLOCATE(BUFFER(NPROMA, NLEV, NFIELDS, NBLOCKS))

!$OMP PARALLEL DO DEFAULT(SHARED), PRIVATE(B) schedule(runtime)
    DO B=1, NBLOCKS
       STATE(B)%T => BUFFER(:,:,1,B)
       STATE(B)%A => BUFFER(:,:,2,B)
       STATE(B)%Q => BUFFER(:,:,3,B)
       STATE(B)%CLD => BUFFER(:,:,4:NFIELDS,B)
    END DO
!$omp end parallel do 
  END SUBROUTINE FIELD_INIT_STATE

  SUBROUTINE CLOUDSC_GLOBAL_STATE_LOAD(SELF, NPROMA, NGPTOT, NGPTOTG)
    ! Load reference input data via serialbox
    CLASS(CLOUDSC_GLOBAL_STATE) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NGPTOT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NGPTOTG

    INTEGER(KIND=JPIM) :: KLON

    CALL INPUT_INITIALIZE(NAME='input', READ_ONLY=.TRUE.)

    SELF%NBLOCKS = (NGPTOT / NPROMA) + MIN(MOD(NGPTOT,NPROMA), 1)
    CALL LOAD_SCALAR('KLON', KLON)
    CALL LOAD_SCALAR('KLEV', SELF%KLEV)

    CALL LOAD_AND_EXPAND('PT', SELF%PT, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PQ', SELF%PQ, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PAP', SELF%PAP, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PAPH', SELF%PAPH, KLON, SELF%KLEV+1, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PLU', SELF%PLU, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PLUDE', SELF%PLUDE, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PMFU', SELF%PMFU, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PMFD', SELF%PMFD, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PA', SELF%PA, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PCLV', SELF%PCLV, KLON, SELF%KLEV, NCLV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PSUPSAT', SELF%PSUPSAT, KLON, SELF%KLEV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)

    ! The STATE_TYPE arrays are tricky, as the AOSOA layout needs to be expictly
    ! unrolled at every step, and we rely on dirty hackery to do this.
    CALL FIELD_INIT(SELF%TENDENCY_LOC, SELF%B_LOC, NPROMA, SELF%KLEV, NCLV, SELF%NBLOCKS)
    CALL LOAD_AND_EXPAND_STATE('TENDENCY_CML', SELF%TENDENCY_CML, SELF%B_CML, &
         & KLON, SELF%KLEV, NCLV, NPROMA, NGPTOT, SELF%NBLOCKS, NGPTOTG)

    ! Output fields are simply allocated and zero'd
    CALL FIELD_INIT(SELF%PCOVPTOT, NPROMA, SELF%KLEV, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFPLSL, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFPLSN, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFHPSL, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)
    CALL FIELD_INIT(SELF%PFHPSN, NPROMA, SELF%KLEV+1, SELF%NBLOCKS)

    ! Initialize global parameters from the input file
    CALL LOAD_SCALAR('PTSPHY', SELF%PTSPHY)
    CALL LOAD_SCALAR('LDSLPHY', SELF%LDSLPHY)
    CALL LOAD_SCALAR('LDMAINCALL', SELF%LDMAINCALL)
    CALL YOMCST_LOAD_PARAMETERS()
    CALL YOETHF_LOAD_PARAMETERS()
    CALL YRECLDP_LOAD_PARAMETERS()
    CALL YREPHLI_LOAD_PARAMETERS()

    CALL INPUT_FINALIZE()

  END SUBROUTINE CLOUDSC_GLOBAL_STATE_LOAD

  SUBROUTINE CLOUDSC_GLOBAL_STATE_VALIDATE(SELF, NPROMA, NGPTOT, NGPTOTG)
    ! Validate the correctness of output against reference data
    CLASS(CLOUDSC_GLOBAL_STATE) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NGPTOT
    INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: NGPTOTG

    REAL(KIND=JPRB), ALLOCATABLE :: PLUDE(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PCOVPTOT(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFPLSL(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFPLSN(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFHPSL(:,:,:)
    REAL(KIND=JPRB), ALLOCATABLE :: PFHPSN(:,:,:)
    TYPE(STATE_TYPE), ALLOCATABLE :: TENDENCY_LOC(:)
    REAL(KIND=JPRB), ALLOCATABLE :: B_LOC(:,:,:,:)

    INTEGER(KIND=JPIM) :: NBLOCKS, KLON, KLEV

    CALL INPUT_INITIALIZE(NAME='reference', READ_ONLY=.TRUE.)

    NBLOCKS = (NGPTOT / NPROMA) + MIN(MOD(NGPTOT,NPROMA), 1)
    CALL LOAD_SCALAR('KLON', KLON)
    CALL LOAD_SCALAR('KLEV', KLEV)

    ! Write variable validation header
    IF (IRANK == 0) THEN
      print '(1X,A20,1X,A3,5(1X,A20))', &
           & 'Variable','Dim', 'MinValue','MaxValue','AbsMaxErr','AvgAbsErr/GP','MaxRelErr-%'
    END IF

    ! Load reference data and expand to our block layout
    CALL LOAD_AND_EXPAND('PLUDE', PLUDE, KLON, SELF%KLEV, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PCOVPTOT', PCOVPTOT, KLON, SELF%KLEV, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFPLSL', PFPLSL, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFPLSN', PFPLSN, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFHPSL', PFHPSL, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND('PFHPSN', PFHPSN, KLON, SELF%KLEV+1, NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL LOAD_AND_EXPAND_STATE('TENDENCY_LOC', TENDENCY_LOC, B_LOC, KLON, SELF%KLEV, NCLV, &
         & NPROMA, NGPTOT, NBLOCKS, NGPTOTG)
    CALL INPUT_FINALIZE()

    ! Actual variable validation
    CALL VALIDATE('PLUDE', PLUDE, SELF%PLUDE, NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PCOVPTOT', PCOVPTOT, SELF%PCOVPTOT, NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFPLSL', PFPLSL, SELF%PFPLSL, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFPLSN', PFPLSN, SELF%PFPLSN, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFHPSL', PFHPSL, SELF%PFHPSL, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('PFHPSN', PFHPSN, SELF%PFHPSN, NPROMA, SELF%KLEV+1, NGPTOT, NBLOCKS, NGPTOTG)
    ! This is extremly hacky, but the array-of-struct-of-array layout demands it.
    CALL VALIDATE('TENDENCY_LOC%A', B_LOC(:,:,2,:), SELF%B_LOC(:,:,2,:), NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('TENDENCY_LOC%Q', B_LOC(:,:,3,:), SELF%B_LOC(:,:,3,:), NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('TENDENCY_LOC%T', B_LOC(:,:,1,:), SELF%B_LOC(:,:,1,:), NPROMA, SELF%KLEV, NGPTOT, NBLOCKS, NGPTOTG)
    CALL VALIDATE('TENDENCY_LOC%CLD', B_LOC(:,:,4:,:), SELF%B_LOC(:,:,4:,:), NPROMA, SELF%KLEV, NCLV, NGPTOT, NBLOCKS, NGPTOTG)

  END SUBROUTINE CLOUDSC_GLOBAL_STATE_VALIDATE

  SUBROUTINE CLOUDSC_GLOBAL_STATE_WRITE_REFERENCE(SELF, KLON)
    ! Load reference input data via serialbox
    CLASS(CLOUDSC_GLOBAL_STATE) :: SELF
    INTEGER(KIND=JPIM), INTENT(INOUT) :: KLON

    IF (KLON /= 100) THEN
      PRINT *, "[CLOUDSC2] Writing reference requires exactly NPROMA=100"
      CALL EXIT(-1)
    END IF

    CALL INPUT_INITIALIZE(NAME='reference', READ_ONLY=.FALSE.)

    CALL WRITE_SCALAR('KLON', KLON)
    CALL WRITE_SCALAR('KLEV', SELF%KLEV)

    CALL WRITE_ARRAY('PLUDE', KLON, SELF%KLEV, SELF%PLUDE(:,:,1))
    CALL WRITE_ARRAY('PCOVPTOT', KLON, SELF%KLEV, SELF%PCOVPTOT(:,:,1))
    CALL WRITE_ARRAY('PFPLSL', KLON, SELF%KLEV+1, SELF%PFPLSL(:,:,1))
    CALL WRITE_ARRAY('PFPLSN', KLON, SELF%KLEV+1, SELF%PFPLSN(:,:,1))
    CALL WRITE_ARRAY('PFHPSL', KLON, SELF%KLEV+1, SELF%PFHPSL(:,:,1))
    CALL WRITE_ARRAY('PFHPSN', KLON, SELF%KLEV+1, SELF%PFHPSN(:,:,1))
    CALL WRITE_ARRAY('TENDENCY_LOC_A', KLON, SELF%KLEV, SELF%B_LOC(:,:,2,1))
    CALL WRITE_ARRAY('TENDENCY_LOC_Q', KLON, SELF%KLEV, SELF%B_LOC(:,:,3,1))
    CALL WRITE_ARRAY('TENDENCY_LOC_T', KLON, SELF%KLEV, SELF%B_LOC(:,:,1,1))
    CALL WRITE_ARRAY('TENDENCY_LOC_CLD', KLON, SELF%KLEV, NCLV, SELF%B_LOC(:,:,4:,1))
    CALL INPUT_FINALIZE()

  END SUBROUTINE CLOUDSC_GLOBAL_STATE_WRITE_REFERENCE


END MODULE CLOUDSC_GLOBAL_STATE_MOD
